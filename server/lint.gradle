import com.thoughtworks.go.build.YarnRunTask

/*
 * Copyright 2018 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

task scssLint(type: Exec) {
  dependsOn ':tools:prepareJRuby'
  description "Run scss-lint"
  def inputDir = project.file("${project.railsRoot}/app/assets/new_stylesheets")
  inputs.dir(inputDir)

  standardOutput = System.out
  errorOutput = System.err

  workingDir = project.railsRoot

  if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
    commandLine = [project.findProject(':tools').file("rails/bin/jruby.bat")]
  } else {
    commandLine = [project.findProject(':tools').file("rails/bin/jruby")]
  }

  commandLine += ['--2.0', '-S', 'bin/scss-lint', "--config", 'scss-lint.yml', inputDir]

  doFirst {
    println commandLine.join(" ")
  }
}

task eslintold(type: YarnRunTask) {
  description "Run ESLint for old javascript code"
  dependsOn 'yarnInstall'

  workingDir = project.railsRoot

  yarnCommand = ['eslintold']
  source(project.file("${project.railsRoot}/app/assets/javascripts"))
}

task eslint(type: YarnRunTask) {
  description "Run ESLint"
  dependsOn 'yarnInstall'

  workingDir = project.railsRoot

  yarnCommand = ['eslint']
  source(project.file("${project.railsRoot}/webpack"))
  source(project.file("${project.railsRoot}/spec/webpack"))
}

task lint {
  description 'Run all lint tools'
  dependsOn eslintold, scssLint, eslint
}
